#! /usr/bin/env perl
# Takes a collection of lines in the form <name>\s+<number>
# and makes a horizontal bar chart of the values that uses the full
# terminal width.
# You can specify a delimiter other than \s+ using the -d flag.

use warnings;
use strict;
use Getopt::Std;

my %opts = ();
getopts('d:', \%opts);

my $delimiter = $opts{'d'} || '\s+';

my @COLORS = 1..6;
my $COLS = int(`tput cols`);

my @entries = ();
while (<>) {
    if (/^\s*(.+?)$delimiter(.+)\s*/) {
        push @entries, [$1, $2];
    }
}

my $max_value = (sort { $a <=> $b } (map { $_->[1] } @entries))[-1];
my $max_lvalue = (sort { $a <=> $b } (map { length($_->[1]) } @entries))[-1];

my $maxbar = $COLS - $max_lvalue - 1;

my $scale = $maxbar / $max_value;

my $i = 0;
for my $entry (@entries) {
    my $name = $entry->[0];
    my $value = $entry->[1];
    my $lname = length($name);
    my $lvalue = length($value);

    my $width = int($value * $scale);
    my $color = $COLORS[$i % @COLORS];

    print
        `tput setab $color` .
        substr($name, 0, $width) .
        ' ' x min0($width - $lname) .
        `tput sgr0` .
        ($lname > $width ? substr($name, $width) : '') .
        `tput setaf $color` .
        '.' x min0($COLS - ($width, $lname)[$width < $lname] - $lvalue - 1) .
        `tput sgr0` .
        " $value\n";

    ++ $i;
}

sub min0 {
    return ($_[0] > 0) ? $_[0] : 0;
}
